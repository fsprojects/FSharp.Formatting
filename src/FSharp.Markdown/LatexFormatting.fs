// --------------------------------------------------------------------------------------
// F# Markdown (LatexFormatting.fs)
// (c) Tomas Petricek, 2012, Available under Apache 2.0 license.
// --------------------------------------------------------------------------------------

module FSharp.Markdown.Latex

open System.IO
open System.Web
open System.Collections.Generic
open FSharp.Patterns
open FSharp.Collections

/// LaTEX special chars
/// from http://tex.stackexchange.com/questions/34580/escape-character-in-latex
let specialChars =
    [|  // This line comes first to avoid double replacing
        // It also accommodates \r, \n, \t, etc.
        @"\", @"<\textbackslash>";
        "#", @"\#";
        "$", @"\$";
        "%", @"\%";
        "&", @"\&";
        "_", @"\_";
        "{", @"\{";
        "}", @"\}";
        @"<\textbackslash>", @"{\textbackslash}";
        "~",@"{\textasciitilde}";
        "^", @"{\textasciicircum}" |]
    
let latexEncode s =
    specialChars |> Array.fold (fun (acc:string) (k, v) -> acc.Replace(k, v)) (HttpUtility.HtmlDecode s)

/// Lookup a specified key in a dictionary, possibly
/// ignoring newlines or spaces in the key.
let (|LookupKey|_|) (dict:IDictionary<_, _>) (key:string) = 
  [ key; key.Replace("\r\n", ""); key.Replace("\r\n", " "); 
    key.Replace("\n", ""); key.Replace("\n", " ") ]
  |> Seq.tryPick (fun key ->
    match dict.TryGetValue(key) with
    | true, v -> Some v 
    | _ -> None)

/// Context passed around while formatting the LaTEX
type FormattingContext =
  { LineBreak : unit -> unit
    Newline : string
    Writer : TextWriter
    Links : IDictionary<string, string * option<string>> }

let smallBreak (ctx:FormattingContext) () =
  ctx.Writer.Write(ctx.Newline)
let noBreak (ctx:FormattingContext) () = ()

/// Write MarkdownSpan value to a TextWriter
let rec formatSpan (ctx:FormattingContext) = function 
  | LatexInlineMath(body) -> ctx.Writer.Write(sprintf "$%s$" body)
  | LatexDisplayMath(body) -> ctx.Writer.Write(sprintf "$$%s$$" body)
  | EmbedSpans(cmd) -> formatSpans ctx (cmd.Render())
  | Literal(str) -> ctx.Writer.Write(latexEncode str)
  | HardLineBreak -> ctx.LineBreak(); ctx.LineBreak()

  | AnchorLink _ -> ()
  | IndirectLink(body, _, LookupKey ctx.Links (link, _)) 
  | DirectLink(body, (link, _))
  | IndirectLink(body, link, _) ->
      ctx.Writer.Write(@"\href{")
      ctx.Writer.Write(latexEncode link)
      ctx.Writer.Write("}{")
      formatSpans ctx body
      ctx.Writer.Write("}")

  | IndirectImage(body, _, LookupKey ctx.Links (link, _)) 
  | DirectImage(body, (link, _)) 
  | IndirectImage(body, link, _) ->
      // Use the technique introduced at
      // http://stackoverflow.com/q/14014827
      if not (System.String.IsNullOrWhiteSpace(body)) then
        ctx.Writer.Write(@"\begin{figure}[htbp]\centering")
        ctx.LineBreak()
      ctx.Writer.Write(@"\includegraphics[width=1.0\textwidth]{")
      ctx.Writer.Write(latexEncode link)
      ctx.Writer.Write("}") 
      ctx.LineBreak()
      if not (System.String.IsNullOrWhiteSpace(body)) then
        ctx.Writer.Write(@"\caption{")
        ctx.Writer.Write(latexEncode body)
        ctx.Writer.Write("}")
        ctx.LineBreak()
        ctx.Writer.Write(@"\end{figure}")
        ctx.LineBreak()

  | Strong(body) -> 
      ctx.Writer.Write(@"\textbf{")
      formatSpans ctx body
      ctx.Writer.Write("}")
  | InlineCode(body) -> 
      ctx.Writer.Write(@"\texttt{")
      ctx.Writer.Write(latexEncode body)
      ctx.Writer.Write("}")
  | Emphasis(body) -> 
      ctx.Writer.Write(@"\emph{")
      formatSpans ctx body
      ctx.Writer.Write("}")

/// Write list of MarkdownSpan values to a TextWriter
and formatSpans ctx = List.iter (formatSpan ctx)

/// Write a MarkdownParagrpah value to a TextWriter
let rec formatParagraph (ctx:FormattingContext) paragraph =
  match paragraph with
  | LatexBlock(lines) ->
    ctx.LineBreak(); ctx.LineBreak()
    ctx.Writer.Write("\["); ctx.LineBreak()
    for line in lines do
      ctx.Writer.Write(line)
      ctx.LineBreak()
    ctx.Writer.Write("\]")
    ctx.LineBreak(); ctx.LineBreak()

  | EmbedParagraphs(cmd) -> formatParagraphs ctx (cmd.Render())
  | Heading(n, spans) -> 
      let level = 
        match n with
        | 1 -> @"\section*"
        | 2 -> @"\subsection*"
        | 3 -> @"\subsubsection*"
        | 4 -> @"\paragraph"
        | 5 -> @"\subparagraph"
        | _ -> ""
      ctx.Writer.Write(level + "{")
      formatSpans ctx spans
      ctx.Writer.Write("}")
      ctx.LineBreak()
  | Paragraph(spans) ->
      ctx.LineBreak(); ctx.LineBreak()
      for span in spans do 
        formatSpan ctx span

  | HorizontalRule ->
      // Reference from http://tex.stackexchange.com/q/19579/9623
      ctx.Writer.Write(@"\noindent\makebox[\linewidth]{\rule{\linewidth}{0.4pt}}\medskip")
      ctx.LineBreak()

  | CodeBlock(code) ->
      ctx.Writer.Write(@"\begin{lstlisting}")
      ctx.LineBreak()
      ctx.Writer.Write(code)
      ctx.LineBreak()
      ctx.Writer.Write(@"\end{lstlisting}")
      ctx.LineBreak()

  | TableBlock(headers, alignments, rows) ->
      let aligns = alignments |> List.map (function
        | AlignRight -> "|r"
        | AlignCenter -> "|c"
        | AlignDefault | AlignLeft -> "|l") |> String.concat ""
      ctx.Writer.Write(@"\begin{tabular}{" + aligns + @"|}\hline")
      ctx.LineBreak()

      let bodyCtx = { ctx with LineBreak = noBreak ctx }
      let formatRow (prefix:string) (postfix:string) row = 
        row |> Seq.iteri (fun i cell ->
          if i <> 0 then ctx.Writer.Write(" & ")
          ctx.Writer.Write(prefix)
          cell |> List.iter (formatParagraph bodyCtx) 
          ctx.Writer.Write(postfix) )

      for header in Option.toList headers do
        formatRow @"\textbf{" "}" header
        ctx.Writer.Write(@"\\ \hline\hline")
        ctx.LineBreak()
      for row in rows do
        formatRow "" "" row
        ctx.Writer.Write(@"\\ \hline")
        ctx.LineBreak()
      ctx.Writer.Write(@"\end{tabular}")
      ctx.LineBreak()

  | ListBlock(kind, items) ->
      let tag = if kind = Ordered then "enumerate" else "itemize"
      ctx.Writer.Write(@"\begin{" + tag + "}")
      ctx.LineBreak()
      for body in items do
        ctx.Writer.Write(@"\item ")
        body |> List.iter (formatParagraph ctx)
        ctx.LineBreak()
      ctx.Writer.Write(@"\end{" + tag + "}")
      ctx.LineBreak()

  | QuotedBlock(body) ->
      ctx.Writer.Write(@"\begin{quote}")
      ctx.LineBreak()
      formatParagraphs ctx body
      ctx.Writer.Write(@"\end{quote}")
      ctx.LineBreak()

  | Span spans -> 
      formatSpans ctx spans
  | InlineBlock(code) ->
      ctx.Writer.Write(code)
  ctx.LineBreak()

/// Write a list of MarkdownParagrpah values to a TextWriter
and formatParagraphs ctx paragraphs = 
  let length = List.length paragraphs
  let ctx = { ctx with LineBreak = smallBreak ctx }
  for last, paragraph in paragraphs |> Seq.mapi (fun i v -> (i = length - 1), v) do
    formatParagraph ctx paragraph

/// Format Markdown document and write the result to 
/// a specified TextWriter. Parameters specify newline character
/// and a dictionary with link keys defined in the document.
let formatMarkdown writer newline links = 
  formatParagraphs 
    { Writer = writer
      Links = links
      Newline = newline
      LineBreak = ignore }
